# Copyright (c) 2021-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
import("//foundation/multimedia/histreamer/config.gni")
config("histreamer_presets") {
  include_dirs = [ "//foundation/multimedia/histreamer/engine" ]
  defines = [ "MEDIA_OHOS" ]
  cflags = [
    "-O2",
    "-fPIC",
    "-Wall",
    "-frtti",
    "-fexceptions",
  ]
  cflags_cc = cflags
  if (multimedia_histreamer_enable_recorder) {
    defines += [ "RECORDER_SUPPORT" ]
  }
}

if (hst_is_lite_sys) {
  import("//build/lite/config/component/lite_component.gni")
  lite_library("histreamer") {
    if (ohos_kernel_type == "liteos_m") {
      target_type = "static_library"
      complete_static_lib = true
    } else {
      target_type = "shared_library"
    }
    sources = []
    deps = [
      "engine/plugin/plugins:histreamer_plugin_store",
      "engine/scene/player:histreamer_player",
    ]
    if (multimedia_histreamer_enable_recorder) {
      deps += [ "engine/scene/recorder:histreamer_recorder" ]
    }
    public_deps = [ "//foundation/multimedia/utils/lite:media_common" ]
    public_configs = [ ":hiplayer_external_library_config" ]
  }
  config("hiplayer_external_library_config") {
    include_dirs = [ "//foundation/multimedia/histreamer/interface" ]
  }
} else {
  import("//build/ohos.gni")
  ohos_shared_library("histreamer") {
    sources = []
    deps = [
      "engine/plugin/plugins:histreamer_plugin_store",
      "engine/scene:std_engine_factory",
    ]
    relative_install_dir = "media"
    output_name = "media_engine_histreamer"
    output_extension = "z.so"
    subsystem_name = "multimedia"
    part_name = "multimedia_histreamer"
  }
}
