# Copyright (c) 2021-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//build/lite/config/subsystem/multimedia/config.gni")

  print("histreamer plugin config:enable_histreamer_plugin_ffmpeg_adapter =",
        enable_histreamer_plugin_ffmpeg_adapter)
  print("histreamer plugin config:enable_histreamer_plugin_hdi_adapter =",
        enable_histreamer_plugin_hdi_adapter)
  print("histreamer plugin config:enable_histreamer_plugin_file_source =",
        enable_histreamer_plugin_file_source)
  print("histreamer plugin config:enable_histreamer_plugin_stream_source =",
        enable_histreamer_plugin_stream_source)

  group("histreamer_plugin_store") {
    deps = []
    if (enable_histreamer_plugin_ffmpeg_adapter) {
      deps += [ "ffmpeg_adapter:plugin_ffmpeg_adapter" ]
    }

    if (enable_histreamer_plugin_file_source) {
      deps += [ "source/file_source:plugin_file_source" ]
    }

    if (enable_histreamer_plugin_hdi_adapter) {
      deps += [ "hdi_adapter:plugin_hdi_adapter" ]
    }

    if (enable_histreamer_plugin_stream_source) {
      deps += [ "source/stream_source:plugin_stream_source" ]
    }
  }

  config("gen_plugin_static_header_config") {
    include_dirs = [ "${target_gen_dir}/include/" ]
  }

  action("gen_plugin_static_header") {
    script = "//foundation/multimedia/histreamer/engine/plugin/plugins/plugin_config.py"

    args = [
      rebase_path("//foundation/multimedia/histreamer/engine/plugin/plugins",
                  root_build_dir),
      rebase_path("${target_gen_dir}/include/", root_build_dir),
    ]

    if (ohos_kernel_type == "liteos_m") {
      if (enable_histreamer_plugin_ffmpeg_adapter) {
        args += [
          "FFmpegDemuxer",
          "FFmpegAudioDecoders",
        ]
      }

      if (enable_histreamer_plugin_file_source) {
        args += [ "FileSource" ]
      }

      if (enable_histreamer_plugin_hdi_adapter) {
        args += [ "HdiAuSink" ]
      }

      if (enable_histreamer_plugin_stream_source) {
        args += [ "StreamSource" ]
      }
    }

    outputs = [ "${target_gen_dir}/include/all_plugin_static.h" ]

    public_configs = [ ":gen_plugin_static_header_config" ]
  }
}
