# Copyright (c) 2021-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")

  declare_args() {
    plugin_dynamic_register = false
  }

  group("histreamer_plugin") {
    deps = [
      ":histreamer_plugin_core",
      ":histreamer_plugin_intf",
      "plugins:histreamer_plugin_store",
    ]
  }

  # build plugin interface
  hst_plugin_intf_src = [ "common/plugin_buffer.cpp" ]

  config("hst_plugin_intf_config") {
    include_dirs = [ "//foundation/multimedia/histreamer/engine/plugin" ]
  }

  lite_library("histreamer_plugin_intf") {
    target_type = "shared_library"
    sources = hst_plugin_intf_src
    public_configs = [ ":hst_plugin_intf_config" ]
    public_deps = [
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
    cflags = [ "-Wall" ]
    cflags_cc = cflags
  }

  lite_library("histreamer_plugin_intf_static") {
    target_type = "static_library"
    sources = hst_plugin_intf_src
    public_configs = [ ":hst_plugin_intf_config" ]
    public_deps = [
      "//foundation/multimedia/histreamer/engine/foundation:histreamer_foundation",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
    cflags = [ "-Wall" ]
    cflags_cc = cflags
  }

  # build plugin core
  hst_plugin_core_src = [
    "core/base.cpp",
    "core/audio_sink.cpp",
    "core/video_sink.cpp",
    "core/codec.cpp",
    "core/demuxer.cpp",
    "core/plugin_register.cpp",
    "core/plugin_wrapper.cpp",
    "core/plugin_manager.cpp",
    "core/source.cpp",
  ]

  config("hst_plugin_core_config") {
    include_dirs = [
      "//foundation/multimedia/histreamer/engine/",
      "//foundation/multimedia/histreamer/engine/plugin",
      "//foundation/multimedia/histreamer/engine/foundation",
      "//foundation/multimedia/histreamer/engine/foundation/osal",
      "//foundation/multimedia/utils/lite/interfaces/kits",
    ]
  }

  lite_library("histreamer_plugin_core") {
    target_type = "shared_library"
    defines = [
      "HI3516DV300",
      "OSAL_OHOS",
    ]
    sources = hst_plugin_core_src
    if (plugin_dynamic_register) {
      sources += [ "engine/plugin/core/plugin_loader.cpp" ]
      defines += [ "DYNAMIC_PLUGINS" ]
    }
    public_configs = [ ":hst_plugin_core_config" ]
    public_deps = [
      ":histreamer_plugin_intf",
      "//foundation/multimedia/histreamer/engine/foundation:histreamer_foundation",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
    cflags = [ "-Wall" ]
    cflags_cc = cflags
  }
}
