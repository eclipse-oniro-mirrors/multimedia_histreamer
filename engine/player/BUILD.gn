# Copyright (c) 2021-2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

config("histreamer_player_config") {
  include_dirs = [
    "//foundation/multimedia/histreamer/engine",
    "//foundation/multimedia/histreamer/engine/player",
    "//foundation/multimedia/histreamer/interface",
    "//foundation/multimedia/media_lite/interfaces/innerkits",
    "//foundation/multimedia/media_lite/interfaces/kits/player_lite",
  ]
  if (defined(ohos_lite)) {
    include_dirs += [ "//foundation/multimedia/histreamer/engine/player/lite" ]
  } else {
    include_dirs += [
      "//foundation/multimedia/histreamer/engine/factory",
      "//foundation/multimedia/histreamer/engine/player/standard",
    ]
  }
}

source_set("histreamer_player") {
  sources = [
    "internal/state.cpp",
    "internal/state_machine.cpp",
  ]
  public_configs = [ ":histreamer_player_config" ]
  public_deps = [
    "//foundation/multimedia/histreamer/engine/foundation:histreamer_foundation",
    "//foundation/multimedia/histreamer/engine/pipeline:histreamer_pipeline",
    "//foundation/multimedia/histreamer/engine/utils:histreamer_utils",
  ]
  if (defined(ohos_lite)) {
    sources += [
      "lite/hiplayer.cpp",
      "lite/hiplayer_impl.cpp",
    ]
    public_deps += [ "//foundation/multimedia/utils/lite:media_common" ]
  } else {
    sources += [
      "../factory/hst_engine_factory.cpp",
      "standard/hiplayer_impl.cpp",
      "standard/media_utils.cpp",
    ]
  }
  print("player source: ", sources)
  defines = [ "MEDIA_OHOS" ]
  if (!defined(ohos_lite)) {
    configs -= [
      "//build/config/compiler:no_rtti",
      "//build/config/compiler:no_exceptions",
    ]
  }
  cflags = [
    "-fPIC",
    "-Wall",
  ]
  if (!defined(ohos_lite)) {
    cflags += [
      "-frtti",
      "-fexceptions",
    ]
  }
  cflags_cc = cflags
}
